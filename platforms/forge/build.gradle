buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7.+"
    }
}

plugins {
    id("platform-conventions")
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "eclipse"

repositories {

	// Add common jar output dirs as repo's for the development environment
    flatDir {
		dir '../../common/common-util/build/libs'
		dir '../../common/common-config/build/libs'
		dir '../../common/common-generator/build/libs'		
		dir '../../common/common-customobject/build/libs'		
		dir '../../common/common-core/build/libs'
    }

	maven {
		name = "sk89q maven"
		url = "https://maven.sk89q.com/artifactory/repo"
	}
}

minecraft {
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: "official", version: "1.17.1"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=otg.mixins.json"

            mods {
                openterraingenerator {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"
            arg "-mixin.config=otg.mixins.json"

            mods {
                openterraingenerator {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg"
            args "--mod", "openterraingenerator", "--all", "--output", file("src/generated/resources/")

            arg "-mixin.config=otg.mixins.json"

            mods {
                openterraingenerator {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
    // <shadow configuration>.extendsFrom library
}
minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}



dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.1.0'

    annotationProcessor("org.spongepowered:mixin:0.8.4:processor")

    // Add common projects for compile-time (not picked up by the development 
    // environment by Forge, presumably because they're non-mod jars)

    implementation project(':common:common-util')
    implementation project(':common:common-config')
    implementation project(':common:common-customobject')
    implementation project(':common:common-generator')
    implementation project(':common:common-core')	

    // Add any mod dependencies (these are picked up by the development environment)

    implementation fg.deobf("com.sk89q.worldedit:worldedit-forge-mc1.17.1:7.3.0-SNAPSHOT")

    // Add common projects and non-mod dependencies as libraries, so they're picked up
    // by the development environment

    library 'com.sk89q.worldedit:worldedit-core:7.3.0-SNAPSHOT'

    library 'com.fasterxml.jackson.core:jackson-annotations:2.9.7'
    library 'com.fasterxml.jackson.core:jackson-core:2.9.7'
    library 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.7'
    library 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    library 'org.yaml:snakeyaml:1.23'

    // TODO: Uncomment these to be able to run in the dev environment
    // Unfortunately this doesn't build common before forge tho ><
    //library 'com.pg85.otg:common-util:1.17.1-0.0.22'
    //library 'com.pg85.otg:common-config:1.17.1-0.0.22'
    //library 'com.pg85.otg:common-customobject:1.17.1-0.0.22'
    //library 'com.pg85.otg:common-generator:1.17.1-0.0.22'
    //library 'com.pg85.otg:common-core:1.17.1-0.0.22'
}

tasks {
    processResources {
        def replacements = [
                "version": project.version
        ]
        inputs.properties(replacements)

        filesMatching("META-INF/mods.toml") {
            expand(replacements)
        }
    }

    jar {
        manifest {
            attributes([
                    "MixinConfigs": "otg.mixins.json"
            ])
        }
    }
}

afterEvaluate {
    tasks.reobfJar {
        input.set(shadowJar.archiveFile)
    }
}
shadowJar.finalizedBy("reobfJar")

otgPlatform {
    productionJar.set(shadowJar.archiveFile)
}

// Example configuration to allow publishing using the maven-publish task
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

mixin {
    add sourceSets.main, "otg.refmap.json"
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
