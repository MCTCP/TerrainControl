// Build script for Terrain Control for Forge

buildscript
{
    repositories
    {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven
        {
            url = "http://files.minecraftforge.net/maven"
        }
        maven
        {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

// Project properties
archivesBaseName = "terraincontrol-forge"
description = "TerrainControl for Minecraft Forge"
ext.forgeVersion = "1.11-13.19.1.2199"
minecraft.version = ext.forgeVersion
minecraft.runDir = "run"
minecraft.mappings = "snapshot_20161129"

repositories
{
    mavenCentral()
    mavenLocal()
}

dependencies
{
    compile project(':common')
    compile 'com.google.guava:guava:17.0'
}

// Replace text in mcmod.info
processResources
{
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        // Replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // Copy everything else, that's not the mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

// Include all Terrain Control files in the jar
jar
{
    destinationDir = new File(project.buildDir, "distributions")
    classifier = ''
    from
    {
        project(":common").sourceSets.main.output.files
    }
    manifest
    {
        attributes 'FMLAT': 'terraincontrol_at.cfg'
    }
}
