// Build script for Terrain Control for Bukkit

apply plugin: 'java'

// Project properties
archivesBaseName = "terraincontrol-bukkit"
description = "TerrainControl for Bukkit"
ext.bukkitVersion = "1.12-pre5-SNAPSHOT"

repositories
{
    mavenLocal() // For Spigot Server
    mavenCentral() // For log4j
    maven 
    {   // Fallback for Spigot Server, no official repo exists
        url = "http://rutgerkok.nl/repo" 
    }
    maven
    {   // For Bukkit API, updated by Spigot
        url = "https://hub.spigotmc.org/nexus/content/groups/public/" 
    }
}

dependencies
{
    testCompile 'junit:junit:4+'
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    compile 'org.bukkit:bukkit:' + project.ext.bukkitVersion
    compile 'org.spigotmc:spigot:' + project.ext.bukkitVersion
    compile project(':common')
}

// Replace text in plugin.yml
processResources
{
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version

    // Replace stuff in plugin.yml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'

        // Replace version
        expand 'version': project.version
    }

    // Copy everything else, that's not the plugin.yml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

// Include all Terrain Control files in the jar
jar
{
    destinationDir = new File(project.buildDir, "distributions")
    classifier = ''
    from
    {
        project(":common").sourceSets.main.output.files
    }
}
