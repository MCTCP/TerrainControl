// Build script for OpenTerrainGenerator for Bukkit

apply plugin: 'java'

// Project properties
archivesBaseName = "openterraingenerator-bukkit"
description = "Open Terrain Generator for Bukkit"
ext.bukkitVersion = "1.12.2-R0.1-SNAPSHOT"

repositories
{
    mavenLocal() // For Spigot Server
    mavenCentral() // For log4j
    maven 
    {   // Fallback for Spigot Server, no official repo exists
        url = "http://rutgerkok.nl/repo" 
    }
    maven
    {   // For Bukkit API, updated by Spigot
		url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies
{
    compile 'org.bukkit:bukkit:' + project.ext.bukkitVersion
    compile 'org.spigotmc:spigot:' + project.ext.bukkitVersion
    compile project(':common')
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
}

// Replace text in plugin.yml
processResources
{
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version

    // Replace stuff in plugin.yml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'

        // Replace version
        expand 'version': project.version
    }

    // Copy everything else, that's not the plugin.yml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

// Include all Open Terrain Generator files in the jar
jar
{
    destinationDir = new File(project.buildDir, "distributions")
    classifier = ''
    from
    {
        project(":common").sourceSets.main.output.files
    }
}
